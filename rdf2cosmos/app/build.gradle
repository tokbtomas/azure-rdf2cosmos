/**
 * Gradle application configuration file.
 * Chris Joakim, Microsoft, January 2022
 */

plugins {
    id 'java'
    id 'application'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.jena:jena-fuseki-fulljar:4.3.2'
    implementation 'org.apache.tinkerpop:gremlin-driver:3.4.0'
    implementation 'org.postgresql:postgresql:42.2.12'
    implementation 'com.google.guava:guava:30.1.1-jre'
    testImplementation 'junit:junit:4.13.2'
}

application {
    mainClass = 'org.cjoakim.rdf2cosmos.App'
}

test {
    useJUnit()
    maxHeapSize = '1G'
    testLogging {
        events "PASSED", "SKIPPED", "FAILED"
    }
}

// $ gradle displayAppConfig
task displayAppConfig(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'org.cjoakim.rdf2cosmos.App'
    args 'app_config'
}

// $ gradle clearDiskCache
task clearDiskCache(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'org.cjoakim.rdf2cosmos.App'
    args 'clear_cache', 'local-disk'
}

// $ gradle clearPostgresqlCache
task clearPostgresqlCache(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'org.cjoakim.rdf2cosmos.App'
    args 'clear_cache', 'az-postgresql'
}

// $ gradle testPostgresqlCache
task testPostgresqlCache(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'org.cjoakim.rdf2cosmos.PostgresqlCache'
    args 'x'
}

tasks.register('uberJar', Jar) {
    archiveClassifier = 'uber'
    duplicatesStrategy = 'include'
    manifest {
        attributes 'Main-Class': "org.cjoakim.rdf2cosmos.App"
    }
    from sourceSets.main.output
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}
